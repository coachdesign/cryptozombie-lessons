---
title: توکن ها در اتریوم
actions: ['بررسی پاسخ', 'راهنمایی']
requireLogin: true
material:
  editor:
    language: sol
    startingCode:
      "zombieownership.sol": |
        // Start here
      "zombieattack.sol": |
        pragma solidity ^0.4.25;

        import "./zombiehelper.sol";

        contract ZombieAttack is ZombieHelper {
          uint randNonce = 0;
          uint attackVictoryProbability = 70;

          function randMod(uint _modulus) internal returns(uint) {
            randNonce++;
            return uint(keccak256(abi.encodePacked(now, msg.sender, randNonce))) % _modulus;
          }

          function attack(uint _zombieId, uint _targetId) external ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            Zombie storage enemyZombie = zombies[_targetId];
            uint rand = randMod(100);
            if (rand <= attackVictoryProbability) {
              myZombie.winCount++;
              myZombie.level++;
              enemyZombie.lossCount++;
              feedAndMultiply(_zombieId, enemyZombie.dna, "zombie");
            } else {
              myZombie.lossCount++;
              enemyZombie.winCount++;
              _triggerCooldown(myZombie);
            }
          }
        }

      "zombiehelper.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefeeding.sol";

        contract ZombieHelper is ZombieFeeding {

          uint levelUpFee = 0.001 ether;

          modifier aboveLevel(uint _level, uint _zombieId) {
            require(zombies[_zombieId].level >= _level);
            _;
          }

          function withdraw() external onlyOwner {
            address _owner = owner();
            _owner.transfer(address(this).balance);
          }

          function setLevelUpFee(uint _fee) external onlyOwner {
            levelUpFee = _fee;
          }

          function levelUp(uint _zombieId) external payable {
            require(msg.value == levelUpFee);
            zombies[_zombieId].level++;
          }

          function changeName(uint _zombieId, string _newName) external aboveLevel(2, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].name = _newName;
          }

          function changeDna(uint _zombieId, uint _newDna) external aboveLevel(20, _zombieId) ownerOf(_zombieId) {
            zombies[_zombieId].dna = _newDna;
          }

          function getZombiesByOwner(address _owner) external view returns(uint[]) {
            uint[] memory result = new uint[](ownerZombieCount[_owner]);
            uint counter = 0;
            for (uint i = 0; i < zombies.length; i++) {
              if (zombieToOwner[i] == _owner) {
                result[counter] = i;
                counter++;
              }
            }
            return result;
          }

        }

      "zombiefeeding.sol": |
        pragma solidity ^0.4.25;

        import "./zombiefactory.sol";

        contract KittyInterface {
          function getKitty(uint256 _id) external view returns (
            bool isGestating,
            bool isReady,
            uint256 cooldownIndex,
            uint256 nextActionAt,
            uint256 siringWithId,
            uint256 birthTime,
            uint256 matronId,
            uint256 sireId,
            uint256 generation,
            uint256 genes
          );
        }

        contract ZombieFeeding is ZombieFactory {

          KittyInterface kittyContract;

          modifier ownerOf(uint _zombieId) {
            require(msg.sender == zombieToOwner[_zombieId]);
            _;
          }

          function setKittyContractAddress(address _address) external onlyOwner {
            kittyContract = KittyInterface(_address);
          }

          function _triggerCooldown(Zombie storage _zombie) internal {
            _zombie.readyTime = uint32(now + cooldownTime);
          }

          function _isReady(Zombie storage _zombie) internal view returns (bool) {
              return (_zombie.readyTime <= now);
          }

          function feedAndMultiply(uint _zombieId, uint _targetDna, string _species) internal ownerOf(_zombieId) {
            Zombie storage myZombie = zombies[_zombieId];
            require(_isReady(myZombie));
            _targetDna = _targetDna % dnaModulus;
            uint newDna = (myZombie.dna + _targetDna) / 2;
            if (keccak256(abi.encodePacked(_species)) == keccak256(abi.encodePacked("kitty"))) {
              newDna = newDna - newDna % 100 + 99;
            }
            _createZombie("NoName", newDna);
            _triggerCooldown(myZombie);
          }

          function feedOnKitty(uint _zombieId, uint _kittyId) public {
            uint kittyDna;
            (,,,,,,,,,kittyDna) = kittyContract.getKitty(_kittyId);
            feedAndMultiply(_zombieId, kittyDna, "kitty");
          }
        }

      "zombiefactory.sol": |
        pragma solidity ^0.4.25;

        import "./ownable.sol";

        contract ZombieFactory is Ownable {

            event NewZombie(uint zombieId, string name, uint dna);

            uint dnaDigits = 16;
            uint dnaModulus = 10 ** dnaDigits;
            uint cooldownTime = 1 days;

            struct Zombie {
              string name;
              uint dna;
              uint32 level;
              uint32 readyTime;
              uint16 winCount;
              uint16 lossCount;
            }

            Zombie[] public zombies;

            mapping (uint => address) public zombieToOwner;
            mapping (address => uint) ownerZombieCount;

            function _createZombie(string _name, uint _dna) internal {
                uint id = zombies.push(Zombie(_name, _dna, 1, uint32(now + cooldownTime), 0, 0)) - 1;
                zombieToOwner[id] = msg.sender;
                ownerZombieCount[msg.sender]++;
                emit NewZombie(id, _name, _dna);
            }

            function _generateRandomDna(string _str) private view returns (uint) {
                uint rand = uint(keccak256(abi.encodePacked(_str)));
                return rand % dnaModulus;
            }

            function createRandomZombie(string _name) public {
                require(ownerZombieCount[msg.sender] == 0);
                uint randDna = _generateRandomDna(_name);
                randDna = randDna - randDna % 100;
                _createZombie(_name, randDna);
            }

        }

      "ownable.sol": |
        pragma solidity ^0.4.25;

        /**
        * @title Ownable
        * @dev The Ownable contract has an owner address, and provides basic authorization control
        * functions, this simplifies the implementation of "user permissions".
        */
        contract Ownable {
          address private _owner;

          event OwnershipTransferred(
            address indexed previousOwner,
            address indexed newOwner
          );

          /**
          * @dev The Ownable constructor sets the original `owner` of the contract to the sender
          * account.
          */
          constructor() internal {
            _owner = msg.sender;
            emit OwnershipTransferred(address(0), _owner);
          }

          /**
          * @return the address of the owner.
          */
          function owner() public view returns(address) {
            return _owner;
          }

          /**
          * @dev Throws if called by any account other than the owner.
          */
          modifier onlyOwner() {
            require(isOwner());
            _;
          }

          /**
          * @return true if `msg.sender` is the owner of the contract.
          */
          function isOwner() public view returns(bool) {
            return msg.sender == _owner;
          }

          /**
          * @dev Allows the current owner to relinquish control of the contract.
          * @notice Renouncing to ownership will leave the contract without an owner.
          * It will not be possible to call the functions with the `onlyOwner`
          * modifier anymore.
          */
          function renounceOwnership() public onlyOwner {
            emit OwnershipTransferred(_owner, address(0));
            _owner = address(0);
          }

          /**
          * @dev Allows the current owner to transfer control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function transferOwnership(address newOwner) public onlyOwner {
            _transferOwnership(newOwner);
          }

          /**
          * @dev Transfers control of the contract to a newOwner.
          * @param newOwner The address to transfer ownership to.
          */
          function _transferOwnership(address newOwner) internal {
            require(newOwner != address(0));
            emit OwnershipTransferred(_owner, newOwner);
            _owner = newOwner;
          }
        }

    answer: |
      pragma solidity ^0.4.25;

      import "./zombieattack.sol";

      contract ZombieOwnership is ZombieAttack {

      }
---
<div dir="rtl">
  
می‌خوایم درباره **توکن‌ها** صحبت کنیم.

اگر مدتی در دنیای اتریوم زمان کذرونده باشید، احتمالا درباره توکن‌ها خصوصا **_ توکن ERC20_** شنیدین.


اساسا **توکن** در اتریوم یک قرارداد هوشمنده با یک سری قوانین. شامل مجموعه‌ای از توابع استاندارد مثل `transferFrom(address _from, address _to, uint256 _tokenId)` و `balanceOf(address _owner)` است. 

معمولا این نوع قراردادهای هوشمند با یک mapping مثل `mapping(address => uint256) balances`، موجودی هر آدرس رو نگه می‌دارن.

بنابراین یک توکن فقط قراردادیه که شامل یه سری توابع است و همچنین مقدار توکن هر آدرس رو نگه‌‌داری می‌کنه و طبق اون کاربرا می‌تونن به هر آدرسی توکن انتقال بدن.

### چرا مهمه؟

از آنجا که توکن‌های ERC20 مجموعه مشابهی از توابع با اسامی یکسان دارند با همه آنها می‌توان از روش مشابهی ارتباط برقرار کرد.

عبارت بالا به این معنی است که اگر شما اپلیکیشنی بسازید که قادر به برقراری ارتباط با یک توکن ERC20 باشه یعنی با تمامی توکن‌های ERC20 می‌تونه تعامل داشته باشه. به این صورت در آینده توکن‌های بیشتری رو بدون تغییر کد می‌تونین به اپ اضافه کنید. به راحتی می‌تونید آدرس توکن جدید رو در اپ ثبت کنید و اپ شما از اون به بعد از اون توکن استفاده می‌کنه.


مبادله مثالی برای این توکن است. زمانیکه یک تبادل یک توکن ERC20 اضافه می کند، در واقع نیاز داره یک قرارداد هوشمند دیگه برای ارتباط اضافه کنه. کاربران می تونن به اون قرارداد بگن که توکن رو به آدرس کیف پول (wallet) معامله بفرسته و زمانیکه کاربر درخواست برگشت می کنه، معامله می تونه توکن رو به کاربران برگردونه.

معامله فقط باید یکبار این منطق انتقال رو پیاده سازی کنه و بعد از اون هر زمانی که بخواد توکن ERC20  جدید اضافه کنه فقط باید آدرس قرارداد جدید رو به دیتابیس اضافه کنه.


### دیگر استانداردهای توکن 

توکن های ERC20  برای توکن هایی که مانند واحد ارزی عمل می کنند جالب است. اما در بازی کریپتو زامبی ما شاید نتواند به عنوان زامبی باشد.

اول اینکه، زامبی ها مثل ارز قابل تقسیم نیستند- امکان انتقال 0.237 اتریوم وجود داره ولی 0.237 زامبی معنی نمی ده.

دوم، همه زامبی ها یکسان نیستند. زامبی مرحله دویی "**Steve**" که شما ساختی به هیچ وجه قابل مقایسه با زامبی مرحله 732 من "**H4XF13LD MORRIS 💯💯😎💯💯**"  نیست. 

نوع دیگری توکن وجود داره که برای جمع آوری کننده کریپتو مثل کریپتوزامبی مناسبه، اسمش **_ERC721 tokens._** است.

توکن **_ERC721_** قابل قابل مبادله نیست چون فرض بر اینه که هر کدومشون منحصر به فرد و غیرقابل تقسیم است. شما فقط در واحدهای کامل می تونید تبادلشان کنید و هر کدام شناسه منحصربه فردی دارن. بنابراین این ها برای تبادل پذیری زامبی های ما بهترین گزینه هستند.

> در نظر داشته باشید که استفاده از استانداردی مثل ERC721 این مزیت رو داره که ما نیاز به پیاده سازی منطق سپرده در قرادادمون نداریم، که نشون بده بازیکنان چطور می تونن زامبی تبادل کنند. اگر با مشخصات مطابقت داشته باشیم یک شخص دیگه می تونه یک پلتفرم تبادل برای توکن های قابل تبادل ERC721  بسازه و زامبی های ERC721  در آن بستر قابل استفاده باشن. 

## دست به کد شو

در درس بعد می خواهیم بیشتر درباره پیاده سازی ERC721 بخونیم. اما اول بیایید ساختار فایل این فصل رو تنظیم کنیم:

می خواهیم منطق ERC721 رو در یک قرارداد به اسم `ZombieOwnership` ذخیره کنیم.
1. در ابتدای فایل نسخه `pragma` رو مشخص می کنیم.

2. این فایل باید`zombieattack.sol`  رو `import` کنه.

3. قرارداد جدیدی تعریف کنید به اسم`ZombieOwnership`  که از `ZombieAttack` به ارث می بره. داخل قرارداد رو فعلا خالی بگذارید.

</div>
